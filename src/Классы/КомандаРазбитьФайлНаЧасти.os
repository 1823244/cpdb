///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Перем Лог;
Перем	УдалитьИсточник;

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт
	
	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, "Разбить файл на части указанного размера (по умолчанию 50 Мб)");
	Парсер.ДобавитьПозиционныйПараметрКоманды(ОписаниеКоманды, "ПутьКФайлу", "Путь к исходному локальному файлу для разбиения");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "-v","Размер части");
	
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "-hash", "Рассчитывать MD5-хеши файлов частей");
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, "-delsource",	"Удалить исходный файл после выполнения операции");
	
	Парсер.ДобавитьКоманду(ОписаниеКоманды);
	
КонецПроцедуры

Функция ВыполнитьКоманду(Знач ПараметрыКоманды) Экспорт
	
	ПутьКФайлу				= ПараметрыКоманды["ПутьКФайлу"];
	УдалитьИсточник			= ПараметрыКоманды["-delsource"];
	РазбитьНаТома			= ПараметрыКоманды["-v"];
	РассчитыватьХеши		= ПараметрыКоманды["-hash"];
	
	ВозможныйРезультат = МенеджерКомандПриложения.РезультатыКоманд();
	
	РассчитыватьХеши = ?(РассчитыватьХеши = Неопределено, Ложь, Истина);

	Если ПустаяСтрока(ПутьКФайлу) Тогда
		Лог.Ошибка("Не указан путь к файлу для разбиения");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;
	
	ПутьК7ЗИП = Найти7Zip();
	
	Если НЕ ЗначениеЗаполнено(ПутьК7ЗИП) Тогда
		Лог.Ошибка("7-Zip не найден");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;

	Если ЗначениеЗаполнено(РазбитьНаТома) Тогда
		КоличествоОтправляемыхФайлов = ЗапаковатьВАрхив(ПутьК7ЗИП,ПутьКФайлу, РазбитьНаТома, РассчитыватьХеши);
	Иначе
		КоличествоОтправляемыхФайлов = ЗапаковатьВАрхив(ПутьК7ЗИП,ПутьКФайлу,, РассчитыватьХеши);
	КонецЕсли;
	
	SetEnvironmentVariable("Amount_splitted_parts", КоличествоОтправляемыхФайлов);
	
	Возврат ВозможныйРезультат.Успех;
КонецФункции

//***************************************
// Функция поиска архиватора
//***************************************
Функция Найти7ZIP()
	// Предполагаем, что для X64_86 7-Zip будет 64-битный
//	ПутьПрограмм = ПолучитьПеременнуюСреды("PROGRAMFILESW6432");
//	Если НЕ ЗначениеЗаполнено(ПутьПрограмм) Тогда
//		ПутьПрограмм = ПолучитьПеременнуюСреды("PROGRAMFILES");
//	КонецЕсли;
	
//	Сообщить("Путь поиска: " + ПутьПрограмм);
	Массив7ZIP = НайтиФайлы("C:\Program Files", "7z.exe", True);
	Если Массив7ZIP.Количество() = 0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат Массив7ZIP[0].ПолноеИмя;
	КонецЕсли;
КонецФункции

//***************************************
// Основная функция упаковки/разбиения файла
//***************************************
Функция ЗапаковатьВАрхив(Знач Архиватор, Знач ПутьКФайлу, Знач ПараметрыАрхива = Неопределено, РассчитыватьХеши = Ложь)
	ДанныеИсхФайла = Новый Файл(ПутьКФайлу);
	ИмяАрхива = ДанныеИсхФайла.ИмяБезРасширения + ".7z";
	ИмяФайлаОшибокАрхивации = ДанныеИсхФайла.Путь + "7z_error_messages.txt";
	КомандаАрхиватора = СтрШаблон("""%1"" a  ""%4"" ""%3"" -t7z -v%2 -mx0", Архиватор, 
		?(ЗначениеЗаполнено(ПараметрыАрхива), ПараметрыАрхива, "50m"),
		ПутьКФайлу,
		ИмяАрхива);
	
	
	Лог.Отладка("команда архиватора: " + КомандаАрхиватора);
	КодВозврата = 0;
	ЗапуститьПриложение(КомандаАрхиватора, ДанныеИсхФайла.Путь, истина, КодВозврата);

	Если КодВозврата = 0 Тогда
		МассивФайловЧастей = НайтиФайлы(ДанныеИсхФайла.Путь, ИмяАрхива + ".???", Ложь);
		Лог.Отладка("Всего частей: " + МассивФайловЧастей.Количество());

		Если УдалитьИсточник Тогда
			УдалитьФайлы(ПутьКФайлу);
		КонецЕсли;

		ЗаписьСписка = Новый ЗаписьТекста(ДанныеИсхФайла.Путь + ПолучитьРазделительПути() + ДанныеИсхФайла.ИмяБезРасширения + ".split",
			КодировкаТекста.UTF8, , Ложь);

		Если РассчитыватьХеши Тогда
			ЗаписьХешей = Новый ЗаписьТекста(ДанныеИсхФайла.Путь + ПолучитьРазделительПути() + ДанныеИсхФайла.ИмяБезРасширения + ".hash",
				КодировкаТекста.UTF8, , Ложь);
			РасчетХешей = Новый ХешированиеДанных(ХешФункция.MD5);
		КонецЕсли;

		Для каждого ФайлЧасти Из МассивФайловЧастей Цикл
			ЗаписьСписка.ЗаписатьСтроку(ФайлЧасти.Имя);

			Если РассчитыватьХеши Тогда
				РасчетХешей.ДобавитьФайл(ФайлЧасти.ПолноеИмя);
				ЗаписьХешей.ЗаписатьСтроку(СтрШаблон("%1 %2",ФайлЧасти.Имя, РасчетХешей.ХешСуммаСтрокой));
				РасчетХешей.Очистить();
			КонецЕсли;

		КонецЦикла;
		ЗаписьСписка.Закрыть();

		Если РассчитыватьХеши Тогда
			ЗаписьХешей.Закрыть();
		КонецЕсли;
		Возврат МассивФайловЧастей.Количество();
	Иначе

		Лог.Ошибка("Архивирование завершилось с ошибкой. Код возврата " + КодВозврата);
		ФайлОшибокАрх = Новый Файл(ИмяФайлаОшибокАрхивации);
		Если ФайлОшибокАрх.Существует() Тогда
			ЧтениеФайла = Новый ЧтениеТекста(ИмяФайлаОшибокАрхивации);
			СтрокаФайлаОшибок = ЧтениеФайла.ПрочитатьСтроку();
			Пока СтрокаФайлаОшибок <> Неопределено Цикл
				Лог.Ошибка(СтрокаФайлаОшибок);
				СтрокаФайлаОшибок = ЧтениеФайла.ПрочитатьСтроку();
			КонецЦикла;
			ЧтениеФайла.Закрыть();
			УдалитьФайлы(ИмяФайлаОшибокАрхивации);
		КонецЕсли;

		Возврат 0;
	КонецЕсли
КонецФункции // ЗапаковатьВАрхив(Знач ПутьКФайлу, Знач ПараметрыАрхива)

Лог = Логирование.ПолучитьЛог("ktb.app.cpdb");